-- Table: fact_page_views
-- Primary Key: page_view_id
-- Foreign Keys: page_id, geo_id

config {
  type: "incremental",
  bigquery: {
    partitionBy: "event_date"
  },
  description: "Fact table for page views"
}

pre_operations {
  ${when(incremental(), `
    DELETE FROM ${self()} WHERE event_date BETWEEN DATE_SUB(CURRENT_DATE(), INTERVAL 4 DAY) AND DATE_SUB(CURRENT_DATE(), INTERVAL 1 DAY)
  `)}
}

SELECT DISTINCT ga4.event_date,
                ga4.event_timestamp,
                ga4.event_name,
                ga4.user_pseudo_id,
                ga4.session_id,
                fs.segment_id,
                ga4.event_id AS page_view_id,
                ga4.is_key_event,
                ga4.page_id,
                ga4.page_referrer_id,
                --ga4.screen_name,
                --ga4.screen_class,
                --ga4.screen_id,
                --ga4.screen_type,
                ga4.geo_id,
                ga4.platform,
                ga4.hostname,
                ga4.device_category,
                ga4.mobile_brand_name,
                ga4.browser,
                ga4.browser_version,
                NULL AS exit_page,
                NULL AS is_landing_page,
                NULL AS time_on_page,
                NULL AS engagement_time_seconds
FROM ${ref("stg_ga4_data")}  ga4
LEFT JOIN ${ref("fact_segments")} fs ON ga4.session_id = fs.session_id AND ga4.event_date = fs.event_date
AND ga4.event_timestamp >= fs.segment_begin
AND ga4.event_timestamp < fs.segment_end
WHERE ga4.event_name IN ('page_view', 'screen_view')
${when(incremental(),
    `AND ga4.event_date BETWEEN DATE_SUB(CURRENT_DATE(), INTERVAL 4 DAY) AND DATE_SUB(CURRENT_DATE(), INTERVAL 1 DAY)`)}
